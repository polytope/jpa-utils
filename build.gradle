plugins {
    id 'java-library'
    id 'maven-publish'
    id "eu.davidea.grabver" version "2.0.2"
}
versioning {
    major 1
    minor 0
}

group 'dk.polytope'
version versioning.name

sourceCompatibility = 1.8

java {
    withSourcesJar()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': versioning.name
        )
    }
}

publishing {
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/polytope/jpa-utils")
            credentials {
                username = System.getenv("GITHUB_MVN_USERNAME") ?: System.getenv("MVN_USERNAME_GITHUB")
                password = System.getenv("GITHUB_MVN_TOKEN") ?: System.getenv("MVN_TOKEN_GITHUB")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            pom {
                name = 'JPA Utils'
                description = 'a collection of JPA utilities which are used in Polytope\'s codebases.'
                url = 'https://github.com/polytope/jpa-utils'
            }

            from components.java
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'javax.persistence', name: 'javax.persistence-api', version: '2.2'
}

tasks.create("generatePomPropertiesForJar") {
    File outputDirectory = file("$buildDir/pom-properties")
    outputDirectory.mkdirs()
    File pomProperties = file("$outputDirectory/pom.properties")
    pomProperties.text = """
#Generated by Gradle
#${new Date()}
groupId=${project.group}
artifactId=${project.name}
version=${project.version}
"""
    outputs.file(pomProperties)
}

jar {
    dependsOn("generatePomFileForMavenJavaPublication", "generatePomPropertiesForJar")
    into("META-INF/maven/${project.group}/${project.name}") {
        from(generatePomFileForMavenJavaPublication) {
            rename(".+", "pom.xml")
        }
        from(generatePomPropertiesForJar)
    }
}
