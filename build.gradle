plugins {
    id 'java-library'
    id 'maven-publish'
    id "eu.davidea.grabver" version "2.0.2"
}
versioning {
    major 1
    minor 0
}

group 'dk.polytope'

version versioning.name

sourceCompatibility = 1.8

sourceSets {
    main.java {
        srcDir "src/main/java"
    }
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': versioning.name
        )
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set("sources")
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.hibernate:hibernate-core:5.4.20.Final'
    implementation group: 'javax.persistence', name: 'javax.persistence-api', version: '2.2'
}

publishing {
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/polytope/jpa-utils")
            credentials {
                username = System.getenv("GITHUB_MVN_USERNAME") ?: System.getenv("MVN_USERNAME_GITHUB")
                password = System.getenv("GITHUB_MVN_TOKEN") ?: System.getenv("MVN_TOKEN_GITHUB")
            }
        }
    }

    publications {
        gpr(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            pom {
                name = 'JPA Utils'
                description = 'a collection of JPA utilities which are used in Polytope\'s codebases.'
                url = 'https://github.com/polytope/jpa-utils'
            }

            from components.java
            afterEvaluate {
                artifact sourcesJar
            }
        }
    }
}


